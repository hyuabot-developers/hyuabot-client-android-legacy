plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "com.apollographql.apollo3" version("3.3.2")
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id "com.google.gms.google-services"
    id 'kotlin-parcelize'
}
apply plugin: "androidx.navigation.safeargs.kotlin"
Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())


android {
    assetPacks = [":fast_follow_pack"]

    compileSdk 33

    signingConfigs {
        debugWithSignedKey {
            storeFile file(properties["SIGNED_STORE_FILE"])
            storePassword properties["SIGNED_STORE_PASSWORD"]
            keyAlias properties["SIGNED_KEY_ALIAS"]
            keyPassword properties["SIGNED_KEY_PASSWORD"]
        }
        releaseWithSignedKey {
            storeFile file(properties["SIGNED_STORE_FILE"])
            storePassword properties["SIGNED_STORE_PASSWORD"]
            keyAlias properties["SIGNED_KEY_ALIAS"]
            keyPassword properties["SIGNED_KEY_PASSWORD"]
        }
    }

    defaultConfig {
        applicationId "app.kobuggi.hyuabot"
        minSdk 26
        targetSdk 33
        versionCode 219
        versionName "2.1.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "admob_id", properties["ADMOB_ID"]
        buildConfigField "String", "admob_unit_id", properties["ADMOB_UNIT_ID"]
        buildConfigField "String", "server_url", properties["API_URL"]
        manifestPlaceholders = [mapKey: "${properties.getProperty("MAP_KEY")}"]
    }

    bundle {
        language {
            enableSplit false
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.releaseWithSignedKey
            ndk {
                debugSymbolLevel 'FULL'
            }
        }

        debug {
            signingConfig signingConfigs.debugWithSignedKey
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
    }
    dataBinding {
        enabled = true
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "armeabi"
            universalApk false
        }
    }

    apollo {
        packageName.set("app.kobuggi.hyuabot")
        generateKotlinModels = true
    }
    namespace 'app.kobuggi.hyuabot'
    buildToolsVersion '33.0.0'
}

dependencies {
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "com.google.android.material:material:1.7.0"
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.android.gms:play-services-location:21.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    // Hilt
    implementation "com.google.dagger:hilt-android:2.42"
    kapt "com.google.dagger:hilt-android-compiler:2.42"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    // Room
    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-paging:$room_version"

    // RxJava
    implementation 'io.reactivex.rxjava3:rxjava:3.1.5'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'

    // Retrofit2
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.github.akarnokd:rxjava3-retrofit-adapter:3.0.0'

    // UI
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'androidx.fragment:fragment-ktx:1.5.4'
    implementation 'androidx.cardview:cardview:1.0.0'

    // Apollo GraphQL
    implementation 'com.apollographql.apollo3:apollo-runtime:3.3.2'

    // ViewPager
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // Datastore
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "androidx.datastore:datastore-preferences-rxjava3:1.0.0"

    // Play Asset Delivery
    implementation 'com.google.android.play:asset-delivery:2.0.1'
    implementation 'com.google.android.play:asset-delivery-ktx:2.0.0'

    // Map
    implementation 'com.google.maps.android:android-maps-utils:2.3.0'

    // Calendar
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.0'
    implementation 'com.github.kizitonwose:CalendarView:1.0.4'

    // Google Analytics
    implementation platform('com.google.firebase:firebase-bom:30.3.2')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx:23.1.0'

    // Google Admob
    implementation 'com.google.android.gms:play-services-ads:21.3.0'
}